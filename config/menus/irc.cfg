irccurident = 0
enterlobby = 0

new_ui lobby [
    ui_header "lobby"
    if $enterlobby [
        if (! (ircconnected freenode) ) [
            enterlobby = 0
            clear_ui 1
        ]
        ircgui freenode
        ui_visible [ ui_tip (format "press %1 to open this chat window again at any time" (dobindsearch "show_ui lobby")) ]
    ] [
        ui_font "emphasis" [ ui_center [ ui_text "RED ECLIPSE CHAT LOBBY" ] ]
        ui_strut 0.5
        ui_center [ ui_text "You are about to enter the main project chat room." ]
        ui_strut 0.5
        ui_center [ ui_text "From there, you can talk with other connected players or ask questions." ]
        ui_center [ ui_text  (format "To access the lobby again at any time, press %1" (dobindsearch "show_ui lobby"))]
        ui_strut 0.5
        ui_center [ ui_text "Are you sure you wish to continue?" ]
        ui_strut 0.5
        ui_center [ ui_stay_open [
            ui_button "^fgYES, continue" [enterlobby = 1; ircaddclient freenode irc.freenode.net 6667 (getplayername); ircaddchan freenode #redeclipse ]
            ui_spring 1
            ui_button "^foNO, go back" [clear_ui 1]
        ] ]
    ]
]

new_ui irc [
    ui_stay_open [
        ui_text "saved identities"
        ui_slider irccurident 0 10
        ui_list [
            ui_list [
                ui_text "network  "
                ui_text "address  "
                ui_text "port     "
                ui_text "nickname "
                ui_text "channel  "
            ]
            ui_list [
                ui_list [
                    ui_textfield ircname@irccurident -40
                    ui_strut 1
                    ui_button "connect" [
                        ircaddclient $[ircname@irccurident] $[irchost@irccurident] $[ircport@irccurident] $[ircnick@irccurident]
                        ircaddchan $[ircname@irccurident] $[ircchan@irccurident]
                    ]
                ]
                ui_textfield [irchost@irccurident] -40
                ui_textfield [ircport@irccurident] -40
                ui_textfield [ircnick@irccurident] -40
                ui_list [
                    ui_textfield [ircchan@irccurident] -40
                    ui_strut 1
                    ui_ui_button "join" [ ircaddchan $[ircname@irccurident] $[ircchan@irccurident] ]
                ]
            ]
        ]
    ]
    ui_bar
    ircgui
] [
    if (= $ui_passes 0) [
        ircname0 = freenode
        irchost0 = chat.freenode.net
        ircport0 = 6667
        ircnick0 = (filter (getplayername) 1 1 0)
        ircchan0 = #re-legacy
    ]
]
