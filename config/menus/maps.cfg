resetmapgui = 0
mapselected = 0
mutsselected = 0
modeselected = -1
lastmode = -1
lastmuts = 0

mapindex = 0
lastmode = -1
lastmuts = 0
mapnum = -1
mapcur = ""
mappth = ""
maplist = ""
mappath = ""
mapextra = ""
mapimg = ""
mapocta = 0
if (getalias mapfavs) [] [mapfavs = ""]
setpersist mapfavs 1
setcomplete mapfavs 1
if (getalias demofavs) [] [demofavs = ""]
setpersist demofavs 1
setcomplete demofavs 1
if (getalias gamefavs) [] [gamefavs = ""]
setpersist gamefavs 1
setcomplete gamefavs 1
resetmapgui = 1
mapscount = 20
searchfilter = 0

mapsmenuinit = [
    modeselected = -1
    mutsselected = 0
    mapindex = 0
    lastmode = -1
    lastmuts = 0
    mapnum = -1
    mapcur = ""
    mappth = ""
    maplist = ""
    mappath = ""
    mapextra = ""
    mapimg = ""
    mapocta = 0
    resetmapgui = 1
    nummodes = 0
    curmode = 0
    loop g (- $modeidxnum 1) [
        q = (+ $g 1)
        if (! (ismodelocked $q)) [ nummodes = (+ $nummodes 1); curmode = $q ]
    ]
    if (= $nummodes 1) [ modeselected = $curmode ]
]

mapsmenuiter = [
    if (|| [!= $lastmode $modeselected] [!= $lastmuts $mutsselected]) [ resetmapgui = 1 ]
    if (= $resetmapgui 1) [
        maprot = 0
        maplist = ""
        mappath = ""
        if (= $modeselected $modeidxdemo) [
            mutsselected = 0
            demofavs = (sortlist $demofavs a b [<s $a $b])
            filelist = (sortlist (listfiles demos dmo) a b [<s $a $b])
            loop q 2 [
                looplist lcurmap $filelist [
                    if (? $q (< (listfind xcurmap $demofavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $demofavs [stringcmp $xcurmap $lcurmap]) 0)) [
                        append maplist $lcurmap
                        append mappath [demos/@lcurmap]
                    ]
                ]
            ]
            maprot = (listlen $maplist)
            append maplist "?"
            append mappath "?"
        ] [
            mutsselected = (mutscheck $modeselected $mutsselected)
            append maplist "<random>"
            append mappath "<random>"
            mapfavs = (sortlist $mapfavs a b [<s $a $b])
            curlist = (sortlist (? (>= $modeselected $modeidxplay) (getmaplist $modeselected $mutsselected (? (isonline) (listlen (listclients 1)) 0)) $allowmaps) a b [<s $a $b])
            curprev = (sortlist (sublist $previousmaps 0 $maphistory) a b [<s $a $b])
            loop q 2 [
                looplist lcurmap $curlist [
                    if (< (listfind pcurmap $curprev [stringcmp $pcurmap $lcurmap]) 0) [
                        if (? $q (< (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0)) [
                            append maplist $lcurmap
                            append mappath [maps/@lcurmap]
                        ]
                    ]
                ]
            ]
            maprot = (listlen $maplist)
            wantlist = 1
            looplist lcurmap $curprev [
                if $wantlist [
                    append maplist "~"
                    append mappath "~"
                    wantlist = 0
                ]
                append maplist $lcurmap
                append mappath [maps/@lcurmap]
            ]
            wantlist = 1
            filelist = (sortlist (listfiles maps mpz) a b [<s $a $b])
            loop q 2 [
                looplist lcurmap $filelist [
                    if (< (listfind mcurmap $maplist [stringcmp $mcurmap $lcurmap]) 0) [
                        if (? $q (< (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0)) [
                            if $wantlist [
                                append maplist "*"
                                append mappath "*"
                                wantlist = 0
                            ]
                            append maplist $lcurmap
                            append mappath [maps/@lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& [> $mapocta 0] [> $hasoctapaks 0]) [
                wantlist = 1
                filelist = (sortlist (listfiles base ogz) a b [<s $a $b])
                loop q 2 [
                    looplist lcurmap $filelist [
                        if (? $q (< (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0) (>= (listfind xcurmap $mapfavs [stringcmp $xcurmap $lcurmap]) 0)) [
                            if $wantlist [
                                append maplist "."
                                append mappath "."
                                wantlist = 0
                            ]
                            append maplist $lcurmap
                            append mappath [base/@lcurmap]
                        ]
                    ]
                ]
            ]
            if (&& (= 1 $searchfilter) (> (stringlen $mapextra) 0)) [
                // apply the search filter and keep special tags $t
                t = "~*.?"
                maplist = (listfilter xcurmap $maplist [ (|| (> (stringstr $xcurmap $mapextra) -1) (> (stringstr $t $xcurmap) -1))])
                mappath = (listfilter xcurmap $mappath [ (|| (> (stringstr $xcurmap $mapextra) -1) (> (stringstr $t $xcurmap) -1))])
                // if there are no maps between two $t tags, remove the first tag
                // if no matching maps are available to vote, begin with a "-" tag
                if (> (stringstr $t (at $maplist 0)) -1) [
                    xcurmap = "-"
                    pcurmap = "-"
                ] [
                    xcurmap = ""
                    pcurmap = ""
                ]
                loop i (listlen $maplist) [
                    if (|| (= (stringstr $t (at $maplist $i)) -1) (= (stringstr $t (at $maplist (+ 1 $i))) -1)) [
                        append xcurmap (at $maplist $i)
                        append pcurmap (at $mappath $i)
                    ]
                ]
                maplist = $xcurmap
                mappath = $pcurmap
            ]
            append maplist "?"
            append mappath "?"
            mapnum = (listfind curmap $maplist [
                || [=s $curmap $mapcur] [=s [maps/@curmap] $mapcur] [
                    && [> $hasoctapaks 0] [> $mapocta 0] [=s [base/@curmap] $mapcur]
                ]
            ])
        ]
        mapcount = (listlen $maplist)
        if (! $maprot) [ maprot = $mapcount ]
        lastmode = $modeselected
        lastmuts = $mutsselected
        mapindex = 0
        resetmapgui = 0
    ]
    if (|| [< $mapnum 0] [>= $mapnum $mapcount]) [
        mapnum = (? (= $modeselected $modeidxdemo) -1 0)
        mapcur = ""
        mappth = ""
        mapdmo = ""
        mapimg = ""
    ] [
    cases (at $maplist $mapnum) "?" [
        mapcur = $mapextra
        if (= $modeselected $modeidxdemo) [
            mappth = [demos/@mapcur]
            mapdmo = (demoscan (format "%1.dmo" $mappth))
            mapimg = (? (>= $mapdmo 0) (format "maps/%1" (demoinfo $mapdmo 1)) "textures/emblem")
        ] [
            mappth = $mapextra
            mapimg = maps/@mapextra
        ]
    ] "<random>" [
        mapcur = "<random>"
        mappth = "<random>"
        mapimg = "question"
    ] () [
        mapcur = (at $maplist $mapnum)
        mappth = (at $mappath $mapnum)
        if (= $modeselected $modeidxdemo) [
            mapdmo = (demoscan (format "%1.dmo" $mappth))
            mapimg = (? (>= $mapdmo 0) (format "maps/%1" (demoinfo $mapdmo 1)) "textures/emblem")
        ] [
            mapimg = (at $mappath $mapnum)
        ]
    ] ]
]

mutsvar = [
    local g m s t
    ui_stay_open [ ui_list [
        g = $$arg2
        m = $$arg3
        s = $$arg5
        if (< $g 0) [ ui_no_skin_fx [ ui_no_cursor_fx [ ui_button $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] ] [
            if (|| (& (mutsimplied $g $m) $arg4) (& $mutslockforce $arg4)) [
                ui_button $arg1 [ implied = @arg4 ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 "checkboxon" $guicheckboxtwocolour
            ] [
                if (ismodelocked $g (| $m $arg4) $arg4 $s) [ ui_no_skin_fx [ ui_no_cursor_fx [ ui_button $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] ] [
                    t = (& $m $arg4)
                    ui_button $arg1 [
                        mutate = @arg4
                        if @t [@@arg3 = @@(mutscheck $g (- $m $arg4))] [
                            @@arg3 = @@(mutscheck $g (| $m $arg4) $arg4)
                        ]
                    ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? $t "checkboxon" "") $guicheckboxcolour
                ]
            ]
        ]
    ] ]
]

modevar = [
    local g m s
    ui_stay_open [ ui_list [
        g = $arg4
        m = $$arg3
        s = $$arg5
        if (ismodelocked $g $m 0 $s) [ ui_no_skin_fx [ ui_no_cursor_fx [ ui_button $arg1 [ disabled = @arg4 ] [] "checkdisable" 0x808080 ] ] ] [
            ui_button $arg1 [@arg2 = @arg4] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? (= $$arg2 $arg4) "checkboxon" "") $guicheckboxcolour
        ]
    ] ]
]

mapsexec = [
    sleep 1 [
        if (isconnected) [ show_ui maps 2 ]
        start @arg1 @arg2 @arg3
    ]
]

mapsmenu = [
    ui_list [
        ui_strut 70 1
        ui_list [
            ui_list [
                ui_list [
                    ui_strut 3 1
                    ui_list [
                        ui_strut 38 1
                        if (< $lastmode 0) [
                            ui_font "emphasis" [ ui_text "game select" ]
                            ui_text "please select a mode and map to continue"
                        ] [
                            gname = (gamename $modeselected $mutsselected 0 32)
                            if (&& (stringlen $mapcur) (= $modeselected $modeidxdemo)) [
                                dinfo = (demoscan (format "%1.dmo" $mappth))
                                dmode = (demoinfo $dinfo 2)
                                dmuts = (demoinfo $dinfo 3)
                                dname = (gamename $dmode $dmuts 0 26)
                                gname = (concat $dname "(demo)")
                            ]
                            ui_font "emphasis" [ ui_text $gname ]
                            ui_list [
                                if (stringlen $mapcur) [
                                    if (= $modeselected $modeidxdemo) [
                                        dinfo = (demoscan (format "%1.dmo" $mappth))
                                        dmapname = (demoinfo $dinfo 1)
                                        ui_text " ^farecorded on "
                                        ui_text $dmapname
                                    ] [
                                        ui_text " ^faselected on "
                                        ui_text $mapcur
                                    ]
                                ] [ ui_text "please select a map to continue" ]
                            ]
                        ]
                        ui_strut 0.125
                        ui_list [
                            ui_spring
                            ui_stay_open [ ui_button "^fvpick random" [
                                pickrandom = 1
                                nummodes = 0
                                cntmodes = (- $modeidxnum $modeidxplay)
                                modeselected = (+ (rnd $cntmodes) $modeidxplay)
                                while [&& [ismodelocked $modeselected] [<= $nummodes $cntmodes]] [
                                    modeselected = (+ $modeselected 1)
                                    if (>= $modeselected $modeidxnum) [ modeselected = $modeidxplay ]
                                    nummodes = (+ $nummodes 1)
                                ]
                                mutsselected = (rnd (+ $mutsbitall 1))
                                loop g $mutsidxnum [
                                    q = (<< 1 $g)
                                    if (ismodelocked $modeselected $q) [ mutsselected = (&~ $mutsselected $q) ]
                                ]
                            ] ]
                            ui_spring
                            if (isconnected) [
                                ui_stay_open [
                                    ui_button "^focopy current" [
                                        copycurrent = 1
                                        modeselected = (gamemode)
                                        if (< (mutators) 0) [ mutsselected = 0 ] [ mutsselected = (mutscheck $modeselected (mutators)) ]
                                    ]
                                ]
                                ui_spring
                            ]
                            ui_stay_open [ ui_button "^fyreset selection" [
                                resetselection = 1
                                modeselected = -1
                                mutsselected = 0
                                mapnum = -1
                                mapcur = ""
                                mappth = ""
                                mapimg = ""
                                mapdmo = ""
                            ] ]
                            ui_spring
                        ]
                        ui_strut 0.125
                    ]
                ]
                ui_list [
                    ui_list [
                        ui_font "emphasis" [ ui_text "mode" ]
                        ui_strut 0.25
                        loop z $modeidxnum [
                            modevar (at $modename $z) modeselected mutsselected $z mapcur
                        ]
                    ]
                    ui_strut 3
                    ui_list [
                        ui_stay_open [
                            if (stringlen $mapcur) [ 
                                ui_image $mapimg [mapsexec @mapcur @modeselected @mutsselected ] 5 1 "textures/emblem" [
                                    fav = (format "%1.%2.%3" @mapcur @modeselected @mutsselected)
                                    gamefavs = (concat $fav (listdel $gamefavs $fav)) 
                                    conout "added to favorites list"
                                ]
                            ] [
                                ui_no_skin_fx [ui_image "textures/emblem" [ conout 1 "you have not selected a map" ] 5 1 ]
                            ]
                        ]
                    ]
                ]
                ui_font "emphasis" [ ui_text "mutators" ]
                ui_strut 0.25
                cnt = (- $mutsidxnum $mutsidxgsn)
                ui_loop_split n 3 $cnt [
                    mutsvar (at $mutsname $n) modeselected mutsselected (<< 1 $n) mapcur
                ] [ ui_strut 3 ]
                if (> (stringlen (gspmutname $modeselected 0)) 0) [
                    ui_strut 1
                    ui_font "emphasis" [ ui_text "mode specific mutators" ]
                    ui_strut 0.25
                    ui_loop_split n 3 $mutsidxgsn [
                        mut = (gspmutname $modeselected $n)
                        if (stringlen $mut) [
                            mutsvar $mut modeselected mutsselected (<< 1 (+ $mutsidxgsp $n)) mapcur
                        ]
                    ] [ ui_strut 3 ]
                ]
            ]
            ui_strut 1
            ui_list [
                ui_list [
                    if $modeselected [
                        ui_text "available maps:"
                    ] [
                        ui_text "available demos:"
                    ]
                    if (> $hasoctapaks 0) [
                        ui_spring
                        ui_stay_open [
                            ui_button "show sauer maps" [
                                mapocta = (! $mapocta)
                                resetmapgui = 1
                            ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? (> $mapocta 0) "checkboxon" "") $guicheckboxcolour
                        ]
                        ui_spring 1
                        ui_text "fav"
                        ui_strut 3
                    ]
                ]
                ui_strut 0.25
                ui_list [
                    nummaps = (- (listlen $maplist) 1)
                    ui_list [
                        ui_list [
                            ui_strut $mapscount 1
                            mapindex = (min (max 0 (- $nummaps $mapscount)) $mapindex) //safeguard
                            mapnum = (min $mapnum $nummaps)
                            ui_list [
                                ui_strut 37 1
                                break = 0
                                loopwhile i $mapscount [= $break 0] [
                                    q = (+ $mapindex $i)
                                    curmap = (at $maplist $q)
                                    cases $curmap "*" [
                                        ui_text "maps not in the rotation:"
                                    ] "~" [
                                        ui_text "maps played recently:"
                                    ] "." [
                                        ui_text "maps from sauerbraten:"
                                    ] "-" [
                                        ui_text "^fanone for this search filter" "textures/info"
                                    ] "?" [
                                        break = 1
                                    ] () [
                                        ui_list [
                                            ui_radiobutton (stringreplace $curmap $mapextra (format "^fs^fy%1^fS" $mapextra)) mapnum $q
                                            ui_spring 1
                                            ui_stay_open [
                                                hasmap = (>= (indexof (? (= $modeselected $modeidxdemo) $demofavs $mapfavs) $curmap) 0)
                                                ui_button "" [
                                                    if (= $modeselected $modeidxdemo) [
                                                        demofavs = (? @@hasmap (listdel $demofavs @@curmap) (concat (listdel $demofavs @@curmap) @@curmap))
                                                    ] [
                                                        mapfavs = (? @@hasmap (listdel $mapfavs @@curmap) (concat (listdel $mapfavs @@curmap) @@curmap))
                                                    ]
                                                    resetmapgui = 1
                                                ] [] "checkbox" 0xFFFFFF 0xFFFFFF -1 1 (? $hasmap "checkboxon" "") $guicheckboxcolour
                                            ]
                                        ]
                                    ]
                                ]
                            ]
                        ]
                        ui_list [
                            ui_text "custom map or "
                            ui_checkbox "search filter" searchfilter 1 0 [resetmapgui = 1]
                        ]
                        ui_list [
                            mapextraval = $mapextra
                            mapextranum = $nummaps
                            ui_radiobutton "" mapnum $mapextranum
                            ui_textfield mapextraval 34 [mapextra = $mapextraval; mapnum = $mapextranum; resetmapgui = $searchfilter] -1 0 "" 0 "^fd<enter map name>" $searchfilter
                        ]
                    ]
                    ui_slider mapindex 0 (max (- $nummaps $mapscount) 0) [] 1 1
                ]
            ]
        ]
        ui_strut 1
        ui_list [
            if (! (isonline)) [
                ui_text "server type:"; ui_strut 0.5
                ui_radiobutton "offline" servertype 0; ui_strut 0.5
                ui_radiobutton "private" servertype 1; ui_strut 0.5
                ui_radiobutton "public" servertype 2
            ]
            ui_spring 1
            ui_font "default" [
                if (>= $lastmode 0) [
                    ui_stay_open [
                        if $modeselected [
                            if (getclientcount) [
                                // other players are present
                                if (&& (getclientpriv $getclientnum $vetolock) (=s (getmastermode 1) "veto")) [
                                    ui_button "^fzwyforce new game (veto)" [ mapsexec $mapcur $modeselected $mutsselected ]
                                ] [
                                    ui_button "^fgsubmit this vote" [ mapsexec $mapcur $modeselected $mutsselected ]
                                ]
                            ] [
                                //either playing offline or without other players
                                ui_button "^fgstart a new game" [ mapsexec $mapcur $modeselected $mutsselected ]
                            ]
                            ui_strut 2
                            ui_button "add to favourites" [
                                fav = (format "%1.%2.%3" @mapcur @modeselected @mutsselected)
                                gamefavs = (concat $fav (listdel $gamefavs $fav)) 
                            ] [] "" 0x0080ff
                        ] [
                            ui_button "^fgstart demo playback" [ mapsexec $mapcur $modeselected $mutsselected ]
                        ]
                        ui_strut 14
                    ]
                ] [
                    ui_text "^fy.. pick a mode to continue .."
                    ui_strut 10
                ]
            ]
        ]
        if (&& (getclientpriv $getclientnum $masterlock) (getclientcount)) [
            ui_strut 0.2
            ui_stay_open [ ui_right [
                ui_button (concat "master mode:" (getmastermode 1)) [mastermode (! (getmastermode))]
                ui_strut 13.5
            ] ]
        ]
        ui_visible [
            cases (at $guirolloveraction 0) "modeselected" [
                ui_tooltip (modedesc (at $guirolloveraction 2) $mutsselected 3)
            ] "mutate" [
                ui_tooltip (mutsdesc $modeselected (at $guirolloveraction 2) 3)
            ] "implied" [
                ui_tooltip "this is forced on by the current configuration"
            ] "disabled" [
                ui_tooltip "this is disabled by the current configuration"
            ]
            ui_tip (format "press %1 to open this menu at any time" (dobindsearch "show_ui maps 1"))
        ]
    ]
]

voteindex = 0
votenum = 0
votemenuinit = [ voteindex = 0 ]



votemenu = [
    ui_big_macro vote 8 74 3.2 [1] (getvote) [
        voteplayers = (getvote $i 0)
        votemode = (getvote $i 1)
        votemuts = (getvote $i 2)
        votemap = (getvote $i 3)
        voteself = 0
        loop j $voteplayers [ if (= (getclientnum) (getvote $i 0 $j)) [ voteself = 1 ] ]
    ] [1] [
        ui_button "^fwThere are no votes currently pending, ^fgsubmit ^fwone yourself" [show_ui maps 1] [] "chat"
    ] [
        ui_list [
            ui_checkbox "^fcdynamic sort" sortvotes; ui_strut 1.5
            ui_checkbox "^focleanup list" cleanvotes; ui_strut 1.5
            ui_button "^fyclear vote" clearvote
            ui_spring
            ui_list [
                ui_background $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend
                ui_list [
                    ui_strut 1
                    ui_text (format "^fc%1 ^fwgame%2 suggested" $votenum (? (!= $votenum 1) "s"))
                    ui_strut 1
                ]
            ]
        ]
        ui_strut 0.25
    ] [
        ui_merge 72 [
            ui_center [
                ui_strut 4 1
                votecolour = (? $voteself "^fy" "^fw")
                ui_center [ui_font "default" [ui_button (concatword $votecolour $voteplayers)]]
                ui_center [ui_font "little" [ui_button (format "%1vote%2" $votecolour (? (!= $voteplayers 1) "s"))]]
            ]
            voteimage = "textures/chat"
            votepath = (listfind curmap $maplist [|| [=s $curmap $votemap] [=s [maps/@curmap] $votemap] [
                && [> $mapocta 0] [> $hasoctapaks 0] [=s [base/@curmap] $votemap]
            ]])
            if (> $votepath -1) [ voteimage = (at $mappath $votepath) ]
            ui_strut 1
            ui_image $voteimage "" 1.5 1 "textures/chat"
            ui_strut 1
            ui_center [
                gname = (gamename $votemode $votemuts 0 32)
                ui_button (format "^fy%1 ^favoted for on ^fo%2" $gname $votemap)
                ui_list [
                    if (> $voteplayers 0) [
                        ui_button "^fwby "
                        ui_font "little" [
                            pname = ""
                            pmore = 0
                            loop j $voteplayers [
                                if (|| $pmore (> (ui_text_width $pname) 1220)) [ pmore = (+ $pmore 1) ] [
                                    append pname (format ["%1"] (getclientname (getvote $i 0 $j) 1))
                                ]
                            ]
                            ui_button (concat (prettylist $pname) (? $pmore (concat "and^fy" $pmore "^fwmore")))
                        ]
                    ] [ ui_button "^fano current votes" ]
                ]
            ]
        ] [
            if (= @voteself 1) [ clearvote ] [ start @@votemap @@votemode @@votemuts ]
        ] [
            modeselected = @votemode; mutsselected = @votemuts; mapcur = @votemap; show_ui maps 1
	]
    ] [
        ui_visible [ ui_tip (format "press %1 to open this menu at any time" (dobindsearch "show_ui maps 2")) ]
    ]
]

favindex = 0
favnum = 0


favsmenu = [
    ui_big_macro fav 8 74 3.2 [1] (listlen $gamefavs) [
        favs = (at $gamefavs $i)
        favl = (stringreplace $favs "." " ")
        votemap = (at $favl 0)
        votemode = (at $favl 1)
        votemuts = (at $favl 2) 
        votebad = (ismodelocked $votemode $votemuts $votemuts $votemap)
        if (= -1 (stringstr (getmaplist $votemode $votemuts) $votemap)) [votebad = 1]
    ] [1] [
        ui_button "^fwuse the maps menu to save your favourite game modes." [show_ui maps 1] [] "info"
    ] [
        ui_text (format "Click to %1 a game, right click to copy your selection to the first tab." (? $getclientcount "vote" "start"))
        ui_strut 0.25
    ] [
        ui_strut 0.2
        ui_list [
            ui_body [
                ui_list [
                    voteimage = "textures/question"
                    votepath = (listfind curmap $maplist [|| [=s $curmap $votemap] [=s [maps/@curmap] $votemap] [
                        && [> $mapocta 0] [> $hasoctapaks 0] [=s [base/@curmap] $votemap]
                    ]])
                    if (> $votepath -1) [ voteimage = (at $mappath $votepath) ]
                    ui_strut 1
                    ui_image $voteimage [] 1.5 1 
                    ui_strut 0.5
                    ui_center [
                        gname = (gamename $votemode $votemuts 0 60)
                        if $votebad [
                            ui_text (format " ^fd%1 on %2" $gname $votemap)
                            ui_text " ^frthis game is currently not allowed"
                        ] [
                            ui_button (format " ^fy%1 ^faon ^fo%2" $gname $votemap)
                            ui_list [
                                looplist i (modetexlist $votemode $votemuts 1) [ui_image $i [] 0.75]
                            ]
                        ]
                    ]
                ]
            ] [mapsexec @favl] [
		modeselected = @votemode; mutsselected = @votemuts; mapcur = @votemap; show_ui maps 1
            ]
            ui_spring
            ui_center [ui_image $warningtex [gamefavs = (listdel $gamefavs @favs)] 0.8 1 [] "" 0xff3333]
            ui_center [ui_image $arrowtex [gamefavs = (concat @favs (listdel $gamefavs @favs))] 0.8 1]
            ui_center [ui_image $arrowdowntex [gamefavs = (concat (listdel $gamefavs @favs) @favs)] 0.8 1]
        ]
    ] [
        //ui_visible [ ui_tip (format "press %1 to open this menu at any time" (dobindsearch "show_ui maps 3")) ]
    ]
]


new_ui maps [
    octapaks [
        mapsmenuiter
        ui_list [ mapsmenu ]
        if (|| (getclientcount) (getvote)) [
            votesum = 0
            loop j $votenum [ votesum = (+ $votesum (getvote $j 0)) ]
            ui_tab (format "votes (%1)" $votesum) 
            ui_list [ votemenu ]
        ]
        ui_tab (format "favourites (%1)" $favnum)
        ui_list [favsmenu]
    ]
] [
    if (= $guipasses 0) [
        octapaks [ mapsmenuinit; votemenuinit ]
    ]
]
