bind ESCAPE [if (clear_ui 1) [] [show_ui main]]    // it all starts here

showmainplayerprev = 1
setpersist showmainplayerprev 1
setcomplete showmainplayerprev 1

new_ui main [
    ui_two [
        if $showmainplayerprev [ ui_center [
            ui_center [ ui_player_preview (getplayermodel) (getplayercolour -1) (getplayerteam 1) (weapselect) (getplayervanity) [show_ui profilediff; show_ui profile] 7.5 1 1]
            ui_center [ ui_text (getplayername) ]
        ] ]
    ] [
        ui_list [ ui_strut 30 ]
        ui_center [ ui_list [ ui_font "emphasis" [
            ui_center [ ui_button "profile" "show_ui profilediff; show_ui profile" ]
            ui_center [ ui_button "user account" "show_ui useraccount" ]
            ui_strut 0.5
            if (&& (isconnected) (> (getvote) 0)) [
                ui_center [ ui_button "show votes" "show_ui maps 2" ]
                ui_strut 0.5
            ]
            ui_center [ ui_button (? (isonline) "find servers" "play online") "showservers" ]
            ui_center [ ui_button (? (isconnected) "vote map/mode" "offline practice") "show_ui maps 1" ]
            ui_strut 0.5
            if (&& (isconnected) (= (gamemode) $modeidxediting)) [
                ui_center [ ui_button "editing" "show_ui edit" ]
                ui_strut 0.5
            ]
            ui_center [ ui_button "options" "show_ui options" ]
            ui_center [ ui_button "chat lobby" "show_ui lobby" ]
            ui_center [ ui_button "variables" "show_ui vars" ]
            ui_center [ ui_button "help" "show_ui help" ]
            ui_strut 0.5
            if (isconnected) [
                ui_center [ ui_button "disconnect" "savewarnchk disconnect" ]
                ui_strut 0.5
            ]
            ui_center [ ui_button "quit" "savewarnchk quit" ]
        ] ] ]
    ]
    cases $guirolloveraction "showservers" [
        ui_tooltip "display the server browser for online matches"
    ] "show_ui profilediff; show_ui profile" [
        ui_tooltip "edit your name, colour, loadout and model type"
    ] "show_ui edit" [
        ui_tooltip "access commands to use while editing a map"
    ] "show_ui lobby" [
        ui_tooltip "connect to our irc channel or show open irc tabs"
    ]  "savewarnchk disconnect" [
        ui_tooltip "disconnect your current connection"
    ] "show_ui maps 2" [
        ui_tooltip "display the current votes"
    ] "show_ui maps 1" [
        ui_tooltip (format "select a map/mode combination to %1" (? (isonline) "vote for" "practice on"))
    ] "show_ui options" [
        ui_tooltip "edit your configuration options"
    ] "show_ui vars" [
        ui_tooltip "search for variables, commands or aliases and edit a plethora of game and client settings."
    ] "show_ui help" [
        ui_tooltip "learn more about many aspects of the game"
    ] "savewarnchk quit" [
        ui_tooltip "close Red Eclipse Legacy"
    ]
    ui_tip
]

guimodes = [
    ui_center [ ui_text (modedesc @arg1 @arg2 5) ]
    if (> $arg2 0) [
        loop i $mutsidxnum [
            mut = (<< 1 $i)
            if (&& [& $arg2 $mut] [! (& (mutsimplied $arg1) $mut)]) [
                muttxt = (mutsdesc $arg1 $mut 4)
                if (stringlen $muttxt) [ ui_center [ ui_text (concatword "^fa" $muttxt) ] ]
            ]
        ]
    ]
]

new_ui loading [
    ui_stay_open [ ui_no_hit_fx [
        ui_one [
            ui_font "huge" [ ui_center [ ui_text (? (&& (isloadingmap) (stringlen $maptitle)) $maptitle "loading") ] ]
            ui_font "default" [ ui_center [ ui_text (? (&& (isloadingmap) (stringlen $maptitle) (stringlen $mapauthor)) (concat "by" $mapauthor) "please wait..") ] ]
            if (&& (isloadingmap) (isconnected)) [
                if (= (isloadingmap) 2) [
                    ui_center [ ui_text "^fs^fylightmapping^fS" ]
                ] [
                    gname = (gamename (gamemode) (mutators) 0 32)
                    ui_center [ ui_text (format "^fs^fy%1^fS" $gname) ]
                ]
            ]
            case (isloadingmap) 2 [
                ui_strut 0.5
                ui_center [ ui_image "" [] 7 0 ]
            ] 1 [
                if (isconnected) [
                    ui_strut 0.5
                    ui_font "little" [
                        ui_center [
                            modetexs = (modetexlist (gamemode) (mutators))
                            looplist modetex $modetexs [ ui_image $modetex [] 1 0 "textures/blank" ]
                        ]
                        guimodes (gamemode) (mutators)
                    ]
                    ui_strut 0.5
                    if (stringlen $connectname) [
                        ui_center [ ui_text (format "^faon: ^fw%1:[%2]" $connectname $connectport) ]
                        if (&& (isconnected) (stringlen $serverdesc)) [ ui_font "little" [ ui_center [ ui_text (format "^"^fs%1^fS^"" $serverdesc) ] ] ]
                    ]
                ]
            ]
        ]
        ui_tip
    ] ]
]
