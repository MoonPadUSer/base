shown_server_ui = 0
serverinfo_index = 0
serverinfo_wait = 0
serverinfo_retry = ""
serverinfo_password = ""
server_count = 0
serverinfo_disp = 0
serverinfo_players = 0
serverinfo_servers = 0
serverinfo_server_count = 0
serverinfo_ui_time = 0

serverinfo_types = [ "" "status" "name" "port" "qport" "desc" "mode" "muts" "map" "time" "players" "maxplayers" "ping" "priority" ]
serverinfo_modify = [
    serverinfo_time_list = $arg1
    serverinfo_time_absolute = (? (> $serverinfo_time_list 0) $serverinfo_time_list (- 0 $serverinfo_time_list))
    loop i (listlen $serversort) [
        serverinfo_server_type = (at $serversort $i)
        if $serverinfo_server_type [
            serverinfo_s_absolute = (? (> $serverinfo_server_type 0) $serverinfo_server_type (- 0 $serverinfo_server_type))
            if (!= $serverinfo_time_absolute $serverinfo_s_absolute) [ append serverinfo_time_list $serverinfo_server_type ]
        ]
    ]
    serversort $serverinfo_time_list
]
initialise_server_menu = [
    serverinfo_index = 0
    serverinfo_wait = 0
    serverinfo_retry = ""
    serverinfo_password = ""
    serverinfo_players = 0
    serverinfo_servers = 0
    serverinfo_server_count = 0
    serverinfo_ui_time = (getmillis 1)
    shown_server_ui = 0
    update_server_ui = 0
    search_filter = 0
    search_str = ""
]
server_menu_iterator = [
    if (! $shown_server_ui) [
        map_list = ""
        map_path = ""
        loopfiles lcurmap maps mpz [
            if (< (listfind mcurmap $maplist [=s $mcurmap $lcurmap]) 0) [
                append map_list $lcurmap
                append map_path [maps/@lcurmap]
            ]
        ]
        if (> $hasoctapaks 0) [
            loopfiles lcurmap base ogz [
                append map_list $lcurmap
                append map_path [base/@lcurmap]
            ]
        ]
        shown_server_ui = 1
    ]
]
server_menu = [
    serverinfo_pause = 0
    serverinfo_timer = (- (getmillis 1) $serverinfo_ui_time)
    ui_big_macro serverinfo_ 5 96 4 [getserver] (getserver) [
        serverinfo_status = (getserver $i 0 0)
        serverinfo_name = (getserver $i 0 1)
        serverinfo_port = (getserver $i 0 2)
        serverinfo_name_port_id = [@[serverinfo_name]:[@@serverinfo_port]] // previous name was "sinfonpid"
        serverinfo_description = (getserver $i 0 3)
        serverinfo_map_name = (getserver $i 0 4)
        serverinfo_player_count = (getserver $i 0 5)
        serverinfo_players = (? $i (+ $serverinfo_players $serverinfo_player_count) $serverinfo_player_count)
        serverinfo_servers = (? $i (? $serverinfo_player_count (+ $serverinfo_servers 1) $serverinfo_servers) (? $serverinfo_player_count 1 0))
        serverinfo_ping = (getserver $i 0 6)
        serverinfo_last = (getserver $i 0 7)
        serverinfo_handle = (getserver $i 0 8)
        serverinfo_flags = (getserver $i 0 9)
        serverinfo_bran = (getserver $i 0 10)
        serverinfo_priority = (getserver $i 0 11)
        serverinfo_status_flag = 0
        loop fi (stringlen $serverinfo_flags) [
            if (=s (substring $serverinfo_flags $fi 1) "s") [ serverinfo_status_flag = 1 ]
        ]
        // attributes
        serverinfo_attribute = (getserver $i 1 -1)
        serverinfo_game_version = (getserver $i 1 0)
        serverinfo_mode = (getserver $i 1 1)
        serverinfo_mutators = (getserver $i 1 2)
        serverinfo_time = (getserver $i 1 3)
        serverinfo_max_players = (getserver $i 1 4)
        serverinfo_map_string = (getserver $i 1 5)
        serverinfo_variables = (getserver $i 1 6)
        serverinfo_modifications = (getserver $i 1 7)
        serverinfo_version_map = (getserver $i 1 8)
        serverinfo_version_n = (getserver $i 1 9)
        serverinfo_version_player = (getserver $i 1 10)
        serverinfo_version_server = (getserver $i 1 11)
        serverinfo_version_a = (getserver $i 1 12)
        serverinfo_game_st = (getserver $i 1 13)
        serverinfo_game_tl = (getserver $i 1 14)
        serverinfo_offline_time = (? (>= $serverinfo_last 0) (div (max (- (getmillis 1) (- $serverinfo_last (div $serverinfo_ping 2))) 0) 1000) 0)
        serverinfo_active = (? (< $serverinfo_ping $serverwaiting) 1 0)
        serverinfo_server_count = (? $i (+ $serverinfo_server_count $serverinfo_active) $serverinfo_active)
        search_buffer = (concat $serverinfo_description $serverinfo_name_port_id $serverinfo_map_name (gamename $serverinfo_mode $serverinfo_mutators 0) $serverinfo_handle)
        if (> $serverinfo_player_count 0 ) [
            loop j $serverinfo_player_count [
                append search_buffer (getserver $i 2 $j) // the player name
                append search_buffer (getserver $i 3 $j) // and handle
            ]
        ]
        if (=s $serverinfo_name_port_id $serverinfo_retry) [
            if (= $serverinfo_wait 1) [
                if (stringlen $serverinfo_password) [
                    savewarnchk (format "connect %1 %2 %3" $serverinfo_name $serverinfo_port $serverinfo_password)
                    serverinfo_password = ""
                    serverinfo_retry = ""
                    serverinfo_wait = 0
                ]
            ] [
                if (|| [hasauthkey 1] [!= $serverinfo_status 3]) [
                    savewarnchk (format "connect %1 %2" $serverinfo_name $serverinfo_port)
                    serverinfo_password = ""
                    serverinfo_retry = ""
                    serverinfo_wait = 0
                ]
            ]
        ]
    ] [(|| (= $search_filter 0)  (> (stringcasestr $search_buffer $search_str) -1))] [
        if $update_server_ui [
            ui_button "^fwThere are no servers to display, maybe ^fgupdate ^fwthe list?" updatefrommaster [] "info"
        ] [
            sleep 1 [ update_server_ui = 1; updatefrommaster; serverinfo_ui_time = (getmillis) ]
        ]
    ] [
        if (getserver) [ sleep 1 [ updateservers ] ]
        ui_list [
            // menu bar above server list
            ui_button "^fgupdate" updatefrommaster // update ui_button
            ui_strut 1.5
            ui_button "^frreset" clearservers // reset ui_button
            ui_strut 1.5
            ui_button "^fodisconnect" "savewarnchk disconnect" // disconnect ui_button
            ui_strut 1.5
            ui_button "^fmlan connect" "savewarnchk lanconnect" // lan connect ui_button
            ui_strut 1.5
            ui_checkbox "^fcsearch lan" searchlan // search lan checkbox
            ui_strut 1.5
            ui_checkbox (? $pausesortservers "^fdauto sort" "^fbauto sort") autosortservers // auto sort checkbox
            ui_strut 1.5
            ui_button "^fysort now" sortservers // sort now ui_button
            ui_spring 1
            // show how many players are online on how many servers currently
            ui_list [
                ui_background $ui_color_textfield_background $ui_blend_textfield_background $ui_color_textfield_border $ui_blend_textfield_border
                ui_list [
                    ui_strut 1
                    ui_text (format "^fc%1 ^fwplayers on ^fc%2 ^fwof ^fc%3^fa/^fc%4 ^fwserver%5" $serverinfo_players $serverinfo_servers $serverinfo_server_count $server_count (? (!= $server_count 1) "s"))
                    ui_strut 1
                ]
            ]
        ]
        // if a new version is available, show a banner that holds a link where you can download it 
        ui_strut 0.25
        if (> (getversion 3) (getversion 1)) [
            ui_list [ ui_button "^fzoynew version released! ^fwget it now from: ^fcgithub.com/redeclipse-legacy/release/releases/" "" ]
            ui_strut 0.25
        ]
    ] [
        ui_merge 88 [
            ui_center [
                ui_strut 8 1
                if $serverinfo_active [
                    ui_center [
                        ui_font "huge" [ ui_button (format "^fw%1" $serverinfo_player_count) ] // players playing
                        ui_center [ ui_button (format "^fd/^fa%1" $serverinfo_max_players) ] // max players
                    ]
                    ui_strut 0.125
                    ui_center [
                        ui_button (format "^f%1%2" (? (< $serverinfo_ping 200) "g" (? (< $serverinfo_ping 400) "y" "r")) $serverinfo_ping) // ping info
                        ui_font "little" [ ui_button " ^fams" ]
                    ]
                    ui_strut 0.125
                    ui_center [
                        serverinfo_seconds = (? (>= $serverinfo_last 0) (div (- (getmillis 1) $serverinfo_last) 1000) -1) // ??last ping info??
                        ui_font "little" [ ui_button (? (>= $serverinfo_seconds 0) (format "^fa%1 ^fds ago" $serverinfo_seconds) "^fdwaiting..") ]
                    ]
                ] [ ui_center [ ui_font "huge" [ ui_button "?" ] ] ]
            ]
            
            serverinfo_image = "textures/emblem"
            if (&& [< $serverinfo_ping $serverwaiting] [= $serverinfo_game_version (getversion 1)]) [
                serverinfo_path = (listfind curmap $maplist [|| [=s $curmap $serverinfo_map_name] [=s [base/@curmap] $serverinfo_map_name] [
                    && [> $hasoctapaks 0] [=s [base/@curmap] $serverinfo_map_name]
                ]])
                if (> $serverinfo_path -1) [ serverinfo_image = (at $mappath $serverinfo_path) ]
            ]
            ui_image $serverinfo_image "" 2 1 "textures/emblem" // display the server emblem
            ui_strut 1
            ui_center [
                ui_list [
                    ui_button (format "^fw%1" $serverinfo_description)
                    ui_font "little" [ ui_center [ ui_button (format "^fd(^fa%1^fd)" $serverinfo_name_port_id) ] ] 
                    if $serverinfo_handle [
                        ui_font "little" [ ui_center [ ui_button (format "^fd[^fc%1^fd]" $serverinfo_handle) ] ]
                    ]
                    if $serverinfo_status_flag [
                        ui_font "little" [ ui_center [ ui_button "^fd[^fcstatistics^fd]" ] ]
                    ]
                    ui_font "little" [ ui_center [ ui_button (format "^fd[^fcpriority %1^fd]" $serverinfo_priority) ] ]
                ]
                ui_spring 1
                // information beneigh the server title
                if $serverinfo_active [
                    ui_list [
                        ui_strut 0.25
                        if (= $serverinfo_game_version (getversion 1)) [
                            ui_no_hit_fx [ ui_font "default" [
                                case $serverinfo_status 0 [
                                    ui_button "^fgopen" [] [] "textures/servers/default" 0xFFFFFF 0xFFFFFF -1 0 (? (hasflag $serverinfo_flags s) "textures/servers/statistics" "") 0x22FF22 /
                                ] 1 [
                                    ui_button "^fylock" [] [] "textures/servers/locked" 0xFFFFFF 0xFFFFFF -1 0 (? (hasflag $serverinfo_flags s) "textures/servers/statistics" "") 0x22FF22
                                ] 2 [
                                    ui_button "^fmpriv" [] [] "textures/servers/private" 0xFFFFFF 0xFFFFFF -1 0 (? (hasflag $serverinfo_flags s) "textures/servers/statistics" "") 0x22FF22
                                ] 3 [
                                    ui_button "^frfull" [] [] "textures/servers/full" 0xFFFFFF 0xFFFFFF -1 0 (? (hasflag $serverinfo_flags s) "textures/servers/statistics" "") 0x22FF22
                                ] () [
                                    ui_button "^founknown" [] [] "textures/servers/unknown" 0xFFFFFF 0xFFFFFF -1 0 (? (hasflag $serverinfo_flags s) "textures/servers/statistics" "") 0x22FF22
                                ]
                            ] ]
                            if (!= $serverinfo_mode $modeidxediting) [
                                ui_button (format " ^fd[^fc%1^fd:^fw%2^fd]" (at $gamestatename $serverinfo_game_st) (timestr (* (? $serverinfo_player_count (max (- $serverinfo_game_tl $serverinfo_offline_time) 0) $serverinfo_time) 1000) 3))
                            ]
                            gname = (gamename $serverinfo_mode $serverinfo_mutators 0 32)
                            ui_font "little" [ ui_center [ ui_list [
                                ui_button (format " ^fy%1 ^faon ^fo%2" $gname $serverinfo_map_name)
                                ui_button (format " ^fa(%1modified^fa)" (? $serverinfo_modifications (format "^fc%1%% " (precf (*f (divf $serverinfo_modifications $serverinfo_variables) 100) 2)) "^fgun"))
                                if (>= $serverinfo_attribute 13) [ ui_button (format " ^fa[^fc%1.%2.%3-%4%5-%6^fa]" $serverinfo_version_map $serverinfo_version_n $serverinfo_version_player (platname $serverinfo_version_server) $serverinfo_version_a $serverinfo_bran) ]
                            ] ] ]
                        ] [
                            ui_no_hit_fx [ ui_font "default" [ ui_button "^foincompatible" [] [] "textures/servers/failed" ] ]
                            ui_button (concat " ^faserver is using" (? (> $serverinfo_game_version (getversion 1)) "a ^fwnewer" "an ^fdolder") "protocol")
                        ]
                    ]
                    ui_spring 1
                    // display passowrd field
                    ui_list [
                        if (=s $serverinfo_name_port_id $serverinfo_retry) [
                            if (= $serverinfo_status 3) [ ui_button "^fd[ ^fwwaiting for slot ^fd] " ]
                            ui_button "^fwpassword ^fd= " 
                            serverinfo_passwordval = $serverinfo_password
                            ui_textfield serverinfo_passwordval 20 [serverinfo_password = $serverinfo_passwordval]
                        ] [
                            if (> $serverinfo_player_count 0) [
                                ui_strut 0.25
                                serverinfo_player_count = (getserver $i 2)
                                if (> $serverinfo_player_count 0) [
                                    ui_font "little" [
                                        pname = ""
                                        plist = ""
                                        pmore = 0
                                        loop j $serverinfo_player_count [
                                            if (|| $pmore (>= (ui_text_width $plist) 1400)) [ pmore = (+ $pmore 1) ] [
                                                append pname (format ["%1"] (getserver $i 2 $j))
                                                plist = (prettylist $pname)
                                            ]
                                        ]
                                        ui_button (concat $plist (? $pmore (concat "and^fy" $pmore "^fwmore")))
                                        //[] [] "" -1 -1 1900
                                    ]
                                ] [ ui_button "^faplayer info not available" ]
                            ] [ ui_button "^fano players online" ]
                        ]
                    ]
                ] [
                    ui_list [
                        ui_strut 0.25
                        ui_font "default" [ ui_button "^founresponsive" [] [] "textures/servers/failed" ]
                        ui_button " ^faserver is not replying to queries"
                    ]
                ]
            ]
        ] [
            serverinfo_password = ""
            serverinfo_retry = @(escape $serverinfo_name_port_id)
            serverinfo_wait = (! (|| [hasauthkey 1] [!= @@serverinfo_map_string 4]))
        ] [
            serverinfo_password = ""
            serverinfo_retry = @(escape $serverinfo_name_port_id)
            serverinfo_wait = (! (hasauthkey 1))
        ] [
            serverinfo_pause = 1
            if $serverinfo_active [
                serverinfo_player_count = (getserver $i 2)
                if (> $serverinfo_player_count 0) [
                    phover = ""
                    loop j $serverinfo_player_count [
                        if $j [ append phover "^n" ]
                        phandle = (getserver $i 3 $j)
                        if (stringlen $phandle) [
                            append phover (format "%1 (%2)" (getserver $i 2 $j) $phandle)
                        ] [
                            append phover (format "%1" (getserver $i 2 $j))
                        ]
                    ]
                    ui_tooltip $phover
                ] [ ui_tooltip "^fano players online" ]
            ] [ ui_tooltip "^fano information available" ]
        ]
    ] [
        ui_strut 0.5
        ui_list [
            ui_center_z [ ui_text "update interval:" ]
            ui_strut 0.5
            ui_list [
                ui_strut 87.5 1
                ui_list_slider serverupdateinterval "1 2 3 4 5 10 15 20 25 30 35 40 45 50 55 60"
            ]
        ]
        ui_strut 0.5
        ui_list [
            ui_center_z [ ui_checkbox "filter:" search_filter ]
            if (=s $guirollovername "filter:") [
                ui_tooltip "^faFilter by player names or handles, current map, mode or mutators, server description, IP address or port."
            ]
            ui_strut 0.5
            ui_textfield search_strval 18 [search_str = $search_strval; search_filter = 1; serverinfo_index = 0] -1 0 "" 0 "^fd <enter search terms>" 1
            ui_spring 1
            ui_center_z [
                ui_text "sort:"
                ui_strut 0.5
                loop i (listlen $serversort) [
                    serverinfo_server_type = (at $serversort $i)
                    if $serverinfo_server_type [
                        serverinfo_s_absolute = (? (> $serverinfo_server_type 0) $serverinfo_server_type (- 0 $serverinfo_server_type))
                        serverinfo_server_name = (at $serverinfo_types $serverinfo_s_absolute)
                        ui_strut 0.5
                        ui_button (? (> $serverinfo_server_type 0) $serverinfo_server_name [-@serverinfo_server_name]) [
                            serverinfo_modify @serverinfo_server_type
                        ] [
                            serverinfo_modify (- 0 @serverinfo_server_type)
                        ] "" 0x00FFFF
                    ]
                ]
            ]
            ui_spring 1
            ui_center_z [
                ui_font "little" [
                    loop i (listlen $serverinfo_types) [
                        if (&& $i [< (listfind serverinfo_client_type $serversort [= $i (? (> $serverinfo_client_type 0) $serverinfo_client_type (- 0 $serverinfo_client_type))]) 0]) [
                            serverinfo_server_name = (at $serverinfo_types $i)
                            ui_strut 1
                            ui_button $serverinfo_server_name [
                                serverinfo_modify @i
                            ] [
                                serverinfo_modify (- 0 @i)
                            ] "" 0x888888
                        ]
                    ]
                ]
            ]
            ui_spring 1
            ui_center_z [ ui_button "reset" [serversortreset] [serversortreset] "" 0xFF8888 ]
            ui_strut 1
        ]
        ui_strut 0.5
        if (hasauthkey) [
            ui_list [
                ui_center_z [ ui_text "user account:" ]
                ui_spring 1
                ui_center_z [ ui_checkbox (format "identify as ^fs^fc%1^fS on connect" $accountname) authconnect ]
                ui_spring 1
                ui_center_z [ ui_font "little" [ ui_button "(^fs^fredit account^fS)" [show_ui profile 4] ] ]
            ]
        ]
    ]
    if (&& [! $guilayoutpass] [|| (> $serverinfo_timer 30000) (> $serverinfo_server_count (div (* $server_count 3) 4))]) [ if $serverinfo_pause [ if (! $pausesortservers) [ pausesortservers 1 ] ] [ if $pausesortservers [ pausesortservers 0 ] ] ]

    ui_tip (format "press %1 to open this menu at any time" (dobindsearch "showservers"))
]

new_ui servers [
    server_menu_iterator
    ui_list [ server_menu ]
] [
    if (= $guipasses 0) [
        initialise_server_menu
    ]
]

new_ui guidelines [
    ui_editor doc/guidelines.txt -80 22
    ui_textinit doc/guidelines.txt doc/guidelines.txt
    ui_textmode 4
    ui_strut 0.5
    ui_font "emphasis" [
        ui_list [
            ui_button "[ ^fs^foi disagree, stay offline^fS ]" [clear_ui 1]
            ui_spring 1
            ui_button "[ ^fs^fgi agree, play online^fS ]" [connectguidelines 1; @guidelinesaction; guidelinesaction ""]
            ui_strut 3
        ]
    ]
    ui_status "you must read and agree to these guidelines before playing online"
] [ if $connectguidelines [clear_ui 1] ]
showservers = [ guidelinesaction "show_ui servers"; show_ui (? $connectguidelines servers guidelines) ]
