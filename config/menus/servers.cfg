shownservergui = 0
sinfoindex = 0
sinfowait = 0
sinforetry = ""
sinfopass = ""
sinfonum = 0
sinfodisp = 0
sinfoplayers = 0
sinfoservers = 0
sinfonumsrv = 0
sinfouitime = 0

sinfotypes = [ "" "status" "name" "port" "qport" "desc" "mode" "muts" "map" "time" "players" "maxplayers" "ping" "priority" ]
sinfomodify = [
    sinfotlist = $arg1
    sinfotabsl = (? (> $sinfotlist 0) $sinfotlist (- 0 $sinfotlist))
    loop i (listlen $serversort) [
        sinfostype = (at $serversort $i)
        if $sinfostype [
            sinfosabsl = (? (> $sinfostype 0) $sinfostype (- 0 $sinfostype))
            if (!= $sinfotabsl $sinfosabsl) [ append sinfotlist $sinfostype ]
        ]
    ]
    serversort $sinfotlist
]
servermenuinit = [
    sinfoindex = 0
    sinfowait = 0
    sinforetry = ""
    sinfopass = ""
    sinfoplayers = 0
    sinfoservers = 0
    sinfonumsrv = 0
    sinfouitime = (getmillis 1)
    shownservergui = 0
    updateservergui = 0
    searchfilter = 0
    searchstr = ""
]
servermenuiter = [
    if (! $shownservergui) [
        maplist = ""
        mappath = ""
        loopfiles lcurmap maps mpz [
            if (< (listfind mcurmap $maplist [=s $mcurmap $lcurmap]) 0) [
                append maplist $lcurmap
                append mappath [maps/@lcurmap]
            ]
        ]
        if (> $hasoctapaks 0) [
            loopfiles lcurmap base ogz [
                append maplist $lcurmap
                append mappath [base/@lcurmap]
            ]
        ]
        shownservergui = 1
    ]
]
servermenu = [
    sinfopause = 0
    sinfotimer = (- (getmillis 1) $sinfouitime)
    ui_big_macro sinfo 5 96 4 [getserver] (getserver) [
        sinfostat = (getserver $i 0 0)
        sinfoname = (getserver $i 0 1)
        sinfoport = (getserver $i 0 2)
        sinfonpid = [@[sinfoname]:[@@sinfoport]]
        sinfodesc = (getserver $i 0 3)
        sinfomapn = (getserver $i 0 4)
        sinfonump = (getserver $i 0 5)
        sinfoplayers = (? $i (+ $sinfoplayers $sinfonump) $sinfonump)
        sinfoservers = (? $i (? $sinfonump (+ $sinfoservers 1) $sinfoservers) (? $sinfonump 1 0))
        sinfoping = (getserver $i 0 6)
        sinfolast = (getserver $i 0 7)
        sinfohandle = (getserver $i 0 8)
        sinfoflags = (getserver $i 0 9)
        sinfobran = (getserver $i 0 10)
        sinfoprio = (getserver $i 0 11)
        sinfostatsflag = 0
        loop fi (stringlen $sinfoflags) [
            if (=s (substring $sinfoflags $fi 1) "s") [ sinfostatsflag = 1 ]
        ]
        // attrs
        sinfoattr = (getserver $i 1 -1)
        sinfogver = (getserver $i 1 0)
        sinfomode = (getserver $i 1 1)
        sinfomuts = (getserver $i 1 2)
        sinfotime = (getserver $i 1 3)
        sinfomaxp = (getserver $i 1 4)
        sinfomstr = (getserver $i 1 5)
        sinfovars = (getserver $i 1 6)
        sinfomods = (getserver $i 1 7)
        sinfoverm = (getserver $i 1 8)
        sinfovern = (getserver $i 1 9)
        sinfoverp = (getserver $i 1 10)
        sinfovers = (getserver $i 1 11)
        sinfovera = (getserver $i 1 12)
        sinfogmst = (getserver $i 1 13)
        sinfogmtl = (getserver $i 1 14)
        sinfoofft = (? (>= $sinfolast 0) (div (max (- (getmillis 1) (- $sinfolast (div $sinfoping 2))) 0) 1000) 0)
        sinfoactive = (? (< $sinfoping $serverwaiting) 1 0)
        sinfonumsrv = (? $i (+ $sinfonumsrv $sinfoactive) $sinfoactive)
        searchbuffer = (concat $sinfodesc $sinfonpid $sinfomapn (gamename $sinfomode $sinfomuts 0) $sinfohandle)
        if (> $sinfonump 0 ) [
            loop j $sinfonump [
                append searchbuffer (getserver $i 2 $j) // the player name
                append searchbuffer (getserver $i 3 $j) // and handle
            ]
        ]
        if (=s $sinfonpid $sinforetry) [
            if (= $sinfowait 1) [
                if (stringlen $sinfopass) [
                    savewarnchk (format "connect %1 %2 %3" $sinfoname $sinfoport $sinfopass)
                    sinfopass = ""
                    sinforetry = ""
                    sinfowait = 0
                ]
            ] [
                if (|| [hasauthkey 1] [!= $sinfostat 3]) [
                    savewarnchk (format "connect %1 %2" $sinfoname $sinfoport)
                    sinfopass = ""
                    sinforetry = ""
                    sinfowait = 0
                ]
            ]
        ]
    ] [(|| (= $searchfilter 0)  (> (stringcasestr $searchbuffer $searchstr) -1))] [
        if $updateservergui [
            ui_button "^fwThere are no servers to display, maybe ^fgupdate ^fwthe list?" updatefrommaster [] "info"
        ] [
            sleep 1 [ updateservergui = 1; updatefrommaster; sinfouitime = (getmillis) ]
        ]
    ] [
        if (getserver) [ sleep 1 [ updateservers ] ]
        ui_list [
            // menu bar above server list
            ui_button "^fgupdate" updatefrommaster // update ui_button
            ui_strut 1.5
            ui_button "^frreset" clearservers // reset ui_button
            ui_strut 1.5
            ui_button "^fodisconnect" "savewarnchk disconnect" // disconnect ui_button
            ui_strut 1.5
            ui_button "^fmlan connect" "savewarnchk lanconnect" // lan connect ui_button
            ui_strut 1.5
            ui_checkbox "^fcsearch lan" searchlan // search lan checkbox
            ui_strut 1.5
            ui_checkbox (? $pausesortservers "^fdauto sort" "^fbauto sort") autosortservers // auto sort checkbox
            ui_strut 1.5
            ui_button "^fysort now" sortservers // sort now ui_button
            ui_spring 1
            // show how many players are online on how many servers currently
            ui_list [
                ui_background $guifieldbgcolour $guifieldbgblend $guifieldbordercolour $guifieldborderblend
                ui_list [
                    ui_strut 1
                    ui_text (format "^fc%1 ^fwplayers on ^fc%2 ^fwof ^fc%3^fa/^fc%4 ^fwserver%5" $sinfoplayers $sinfoservers $sinfonumsrv $sinfonum (? (!= $sinfonum 1) "s"))
                    ui_strut 1
                ]
            ]
        ]
        // if a new version is available, show a banner that holds a link where you can download it 
        ui_strut 0.25
        if (> (getversion 3) (getversion 1)) [
            ui_list [ ui_button "^fzoynew version released! ^fwget it now from: ^fcgithub.com/redeclipse-legacy/release/releases/" "" ]
            ui_strut 0.25
        ]
    ] [
        ui_merge 88 [
            ui_center [
                ui_strut 8 1
                if $sinfoactive [
                    ui_center [
                        ui_font "huge" [ ui_button (format "^fw%1" $sinfonump) ] // players playing
                        ui_center [ ui_button (format "^fd/^fa%1" $sinfomaxp) ] // max players
                    ]
                    ui_strut 0.125
                    ui_center [
                        ui_button (format "^f%1%2" (? (< $sinfoping 200) "g" (? (< $sinfoping 400) "y" "r")) $sinfoping) // ping info
                        ui_font "little" [ ui_button " ^fams" ]
                    ]
                    ui_strut 0.125
                    ui_center [
                        sinfosecs = (? (>= $sinfolast 0) (div (- (getmillis 1) $sinfolast) 1000) -1) // ??last ping info??
                        ui_font "little" [ ui_button (? (>= $sinfosecs 0) (format "^fa%1 ^fds ago" $sinfosecs) "^fdwaiting..") ]
                    ]
                ] [ ui_center [ ui_font "huge" [ ui_button "?" ] ] ]
            ]
            
            sinfoimage = "textures/emblem"
            if (&& [< $sinfoping $serverwaiting] [= $sinfogver (getversion 1)]) [
                sinfopath = (listfind curmap $maplist [|| [=s $curmap $sinfomapn] [=s [base/@curmap] $sinfomapn] [
                    && [> $hasoctapaks 0] [=s [base/@curmap] $sinfomapn]
                ]])
                if (> $sinfopath -1) [ sinfoimage = (at $mappath $sinfopath) ]
            ]
            ui_image $sinfoimage "" 2 1 "textures/emblem" // display the server emblem
            ui_strut 1
            ui_center [
                ui_list [
                    ui_button (format "^fw%1" $sinfodesc)
                    ui_font "little" [ ui_center [ ui_button (format "^fd(^fa%1^fd)" $sinfonpid) ] ] 
                    if $sinfohandle [
                        ui_font "little" [ ui_center [ ui_button (format "^fd[^fc%1^fd]" $sinfohandle) ] ]
                    ]
                    if $sinfostatsflag [
                        ui_font "little" [ ui_center [ ui_button "^fd[^fcstatistics^fd]" ] ]
                    ]
                    ui_font "little" [ ui_center [ ui_button (format "^fd[^fcpriority %1^fd]" $sinfoprio) ] ]
                ]
                ui_spring 1
                // information beneigh the server title
                if $sinfoactive [
                    ui_list [
                        ui_strut 0.25
                        if (= $sinfogver (getversion 1)) [
                            ui_no_hit_fx [ ui_font "default" [
                                case $sinfostat 0 [
                                    ui_button "^fgopen" [] [] "textures/servers/default" 0xFFFFFF 0xFFFFFF -1 0 (? (hasflag $sinfoflags s) "textures/servers/statistics" "") 0x22FF22 /
                                ] 1 [
                                    ui_button "^fylock" [] [] "textures/servers/locked" 0xFFFFFF 0xFFFFFF -1 0 (? (hasflag $sinfoflags s) "textures/servers/statistics" "") 0x22FF22
                                ] 2 [
                                    ui_button "^fmpriv" [] [] "textures/servers/private" 0xFFFFFF 0xFFFFFF -1 0 (? (hasflag $sinfoflags s) "textures/servers/statistics" "") 0x22FF22
                                ] 3 [
                                    ui_button "^frfull" [] [] "textures/servers/full" 0xFFFFFF 0xFFFFFF -1 0 (? (hasflag $sinfoflags s) "textures/servers/statistics" "") 0x22FF22
                                ] () [
                                    ui_button "^founknown" [] [] "textures/servers/unknown" 0xFFFFFF 0xFFFFFF -1 0 (? (hasflag $sinfoflags s) "textures/servers/statistics" "") 0x22FF22
                                ]
                            ] ]
                            if (!= $sinfomode $modeidxediting) [
                                ui_button (format " ^fd[^fc%1^fd:^fw%2^fd]" (at $gamestatename $sinfogmst) (timestr (* (? $sinfonump (max (- $sinfogmtl $sinfoofft) 0) $sinfotime) 1000) 3))
                            ]
                            gname = (gamename $sinfomode $sinfomuts 0 32)
                            ui_font "little" [ ui_center [ ui_list [
                                ui_button (format " ^fy%1 ^faon ^fo%2" $gname $sinfomapn)
                                ui_button (format " ^fa(%1modified^fa)" (? $sinfomods (format "^fc%1%% " (precf (*f (divf $sinfomods $sinfovars) 100) 2)) "^fgun"))
                                if (>= $sinfoattr 13) [ ui_button (format " ^fa[^fc%1.%2.%3-%4%5-%6^fa]" $sinfoverm $sinfovern $sinfoverp (platname $sinfovers) $sinfovera $sinfobran) ]
                            ] ] ]
                        ] [
                            ui_no_hit_fx [ ui_font "default" [ ui_button "^foincompatible" [] [] "textures/servers/failed" ] ]
                            ui_button (concat " ^faserver is using" (? (> $sinfogver (getversion 1)) "a ^fwnewer" "an ^fdolder") "protocol")
                        ]
                    ]
                    ui_spring 1
                    // display passowrd field
                    ui_list [
                        if (=s $sinfonpid $sinforetry) [
                            if (= $sinfostat 3) [ ui_button "^fd[ ^fwwaiting for slot ^fd] " ]
                            ui_button "^fwpassword ^fd= " 
                            sinfopassval = $sinfopass
                            ui_textfield sinfopassval 20 [sinfopass = $sinfopassval]
                        ] [
                            if (> $sinfonump 0) [
                                ui_strut 0.25
                                sinfopnum = (getserver $i 2)
                                if (> $sinfopnum 0) [
                                    ui_font "little" [
                                        pname = ""
                                        plist = ""
                                        pmore = 0
                                        loop j $sinfopnum [
                                            if (|| $pmore (>= (ui_textwidth $plist) 1400)) [ pmore = (+ $pmore 1) ] [
                                                append pname (format ["%1"] (getserver $i 2 $j))
                                                plist = (prettylist $pname)
                                            ]
                                        ]
                                        ui_button (concat $plist (? $pmore (concat "and^fy" $pmore "^fwmore")))
                                        //[] [] "" -1 -1 1900
                                    ]
                                ] [ ui_button "^faplayer info not available" ]
                            ] [ ui_button "^fano players online" ]
                        ]
                    ]
                ] [
                    ui_list [
                        ui_strut 0.25
                        ui_font "default" [ ui_button "^founresponsive" [] [] "textures/servers/failed" ]
                        ui_button " ^faserver is not replying to queries"
                    ]
                ]
            ]
        ] [
            sinfopass = ""
            sinforetry = @(escape $sinfonpid)
            sinfowait = (! (|| [hasauthkey 1] [!= @@sinfomstr 4]))
        ] [
            sinfopass = ""
            sinforetry = @(escape $sinfonpid)
            sinfowait = (! (hasauthkey 1))
        ] [
            sinfopause = 1
            if $sinfoactive [
                sinfopnum = (getserver $i 2)
                if (> $sinfopnum 0) [
                    phover = ""
                    loop j $sinfopnum [
                        if $j [ append phover "^n" ]
                        phandle = (getserver $i 3 $j)
                        if (stringlen $phandle) [
                            append phover (format "%1 (%2)" (getserver $i 2 $j) $phandle)
                        ] [
                            append phover (format "%1" (getserver $i 2 $j))
                        ]
                    ]
                    ui_tooltip $phover
                ] [ ui_tooltip "^fano players online" ]
            ] [ ui_tooltip "^fano information available" ]
        ]
    ] [
        ui_strut 0.5
        ui_list [
            ui_center_z [ ui_text "update interval:" ]
            ui_strut 0.5
            ui_list [
                ui_strut 87.5 1
                ui_list_slider serverupdateinterval "1 2 3 4 5 10 15 20 25 30 35 40 45 50 55 60"
            ]
        ]
        ui_strut 0.5
        ui_list [
            ui_center_z [ ui_checkbox "filter:" searchfilter ]
            if (=s $guirollovername "filter:") [
                ui_tooltip "^faFilter by player names or handles, current map, mode or mutators, server description, IP address or port."
            ]
            ui_strut 0.5
            ui_textfield searchstrval 18 [searchstr = $searchstrval; searchfilter = 1; sinfoindex = 0] -1 0 "" 0 "^fd <enter search terms>" 1
            ui_spring 1
            ui_center_z [
                ui_text "sort:"
                ui_strut 0.5
                loop i (listlen $serversort) [
                    sinfostype = (at $serversort $i)
                    if $sinfostype [
                        sinfosabsl = (? (> $sinfostype 0) $sinfostype (- 0 $sinfostype))
                        sinfosname = (at $sinfotypes $sinfosabsl)
                        ui_strut 0.5
                        ui_button (? (> $sinfostype 0) $sinfosname [-@sinfosname]) [
                            sinfomodify @sinfostype
                        ] [
                            sinfomodify (- 0 @sinfostype)
                        ] "" 0x00FFFF
                    ]
                ]
            ]
            ui_spring 1
            ui_center_z [
                ui_font "little" [
                    loop i (listlen $sinfotypes) [
                        if (&& $i [< (listfind sinfoctype $serversort [= $i (? (> $sinfoctype 0) $sinfoctype (- 0 $sinfoctype))]) 0]) [
                            sinfosname = (at $sinfotypes $i)
                            ui_strut 1
                            ui_button $sinfosname [
                                sinfomodify @i
                            ] [
                                sinfomodify (- 0 @i)
                            ] "" 0x888888
                        ]
                    ]
                ]
            ]
            ui_spring 1
            ui_center_z [ ui_button "reset" [serversortreset] [serversortreset] "" 0xFF8888 ]
            ui_strut 1
        ]
        ui_strut 0.5
        if (hasauthkey) [
            ui_list [
                ui_center_z [ ui_text "user account:" ]
                ui_spring 1
                ui_center_z [ ui_checkbox (format "identify as ^fs^fc%1^fS on connect" $accountname) authconnect ]
                ui_spring 1
                ui_center_z [ ui_font "little" [ ui_button "(^fs^fredit account^fS)" [show_ui profile 4] ] ]
            ]
        ]
    ]
    if (&& [! $guilayoutpass] [|| (> $sinfotimer 30000) (> $sinfonumsrv (div (* $sinfonum 3) 4))]) [ if $sinfopause [ if (! $pausesortservers) [ pausesortservers 1 ] ] [ if $pausesortservers [ pausesortservers 0 ] ] ]

    ui_tip (format "press %1 to open this menu at any time" (dobindsearch "showservers"))
]

new_ui servers [
    servermenuiter
    ui_list [ servermenu ]
] [
    if (= $guipasses 0) [
        servermenuinit
    ]
]

new_ui guidelines [
    ui_editor doc/guidelines.txt -80 22
    ui_textinit doc/guidelines.txt doc/guidelines.txt
    ui_textmode 4
    ui_strut 0.5
    ui_font "emphasis" [
        ui_list [
            ui_button "[ ^fs^foi disagree, stay offline^fS ]" [clear_ui 1]
            ui_spring 1
            ui_button "[ ^fs^fgi agree, play online^fS ]" [connectguidelines 1; @guidelinesaction; guidelinesaction ""]
            ui_strut 3
        ]
    ]
    ui_status "you must read and agree to these guidelines before playing online"
] [ if $connectguidelines [clear_ui 1] ]
showservers = [ guidelinesaction "show_ui servers"; show_ui (? $connectguidelines servers guidelines) ]
